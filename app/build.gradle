apply plugin: 'com.android.application'

def getGitVersion = { ->
    try {
        return "git -C ${rootDir} describe --tags --always --dirty".execute().text.trim()
    }
    catch (ignored) {
        return "git-not-found"
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "org.simlar"
        minSdkVersion 16
        targetSdkVersion 27
        versionName getGitVersion()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"

    productFlavors {
        push {
            dimension "default"
            versionName = android.defaultConfig.versionName
            buildConfigField("boolean", "IS_GCM_ENABLED", "true")
        }

        alwaysOnline {
            dimension "default"
            versionName = android.defaultConfig.versionName + " (alwaysOnline)"
            buildConfigField("boolean", "IS_GCM_ENABLED", "false")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        checkAllWarnings true
        disable 'GoogleAppIndexingWarning'
        warningsAsErrors true
    }
}

dependencies {
    pushImplementation 'com.google.android.gms:play-services-gcm:11.8.0'
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:support-v13:27.0.2'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.8.10'
    implementation fileTree(dir: 'libs', include: ['linphone-android-release.aar'])
}
