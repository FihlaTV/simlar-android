apply plugin: "com.android.application"

def getGitVersion = { ->
    try {
        return "git -C ${rootDir} describe --tags --always --dirty".execute().text.trim()
    }
    catch (ignored) {
        return "git-not-found"
    }
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "org.simlar"
        minSdkVersion 16
        targetSdkVersion 27
        versionName getGitVersion()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"

    productFlavors {
        push {
            dimension "default"
            versionName = android.defaultConfig.versionName
            buildConfigField("boolean", "IS_FCM_ENABLED", "true")
        }

        alwaysOnline {
            dimension "default"
            versionName = android.defaultConfig.versionName + " (alwaysOnline)"
            buildConfigField("boolean", "IS_FCM_ENABLED", "false")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
    }

    lintOptions {
        checkAllWarnings true
        disable "GoogleAppIndexingWarning"
        warningsAsErrors true
    }
}

ext {
    androidSupportVersion = "27.1.1"
}

dependencies {
    pushImplementation "com.google.firebase:firebase-core:16.0.1"
    pushImplementation "com.google.firebase:firebase-messaging:17.1.0"
    implementation "com.android.support:appcompat-v7:${androidSupportVersion}"
    implementation "com.android.support:support-v4:${androidSupportVersion}"
    implementation "com.android.support:support-v13:${androidSupportVersion}"
    implementation "com.googlecode.libphonenumber:libphonenumber:8.9.9"
    implementation fileTree(dir: "libs", include: ["liblinphone-sdk.aar"])
}

if (useGoogleServices()) {
    println("using push notifications")
    apply plugin: "com.google.gms.google-services"
} else {
    println("not using push notifications")
}
