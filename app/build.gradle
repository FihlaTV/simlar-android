apply plugin: 'com.android.application'

def getGitVersion = { ->
    try {
        return "git -C ${rootDir} describe --tags --always --dirty".execute().text.trim()
    }
    catch (ignored) {
        return "git-not-found"
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "org.simlar"
        minSdkVersion 14
        targetSdkVersion 26
        versionName getGitVersion()
    }

    flavorDimensions "default"

    productFlavors {
        push {
            dimension "default"
            versionName = android.defaultConfig.versionName
            buildConfigField("boolean", "IS_GCM_ENABLED", "true")
        }

        alwaysOnline {
            dimension "default"
            versionName = android.defaultConfig.versionName + " (alwaysOnline)"
            buildConfigField("boolean", "IS_GCM_ENABLED", "false")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        checkAllWarnings true
        disable 'GoogleAppIndexingWarning'
        warningsAsErrors true
    }
}

dependencies {
    pushImplementation 'com.google.android.gms:play-services-gcm:11.6.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:support-v13:26.1.0'
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.8.6'
    implementation fileTree(dir: 'libs', include: ['liblinphone-android-sdk.aar'])
}
