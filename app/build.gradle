apply plugin: 'com.android.application'

def getGitVersion = { ->
    try {
        return "git -C ${rootDir} describe --tags --always --dirty".execute().text.trim()
    }
    catch (ignored) {
        return "git-not-found";
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"

    defaultConfig {
        applicationId "org.simlar"
        minSdkVersion 14
        targetSdkVersion 26
        versionName getGitVersion()
    }

    productFlavors {
        push {
            versionName = android.defaultConfig.versionName
            buildConfigField("boolean", "IS_GCM_ENABLED", "true")
        }

        alwaysOnline {
            versionName = android.defaultConfig.versionName + " (alwaysOnline)"
            buildConfigField("boolean", "IS_GCM_ENABLED", "false")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        checkAllWarnings true
        disable 'GoogleAppIndexingWarning'
        warningsAsErrors true
    }
}

dependencies {
    pushCompile 'com.google.android.gms:play-services-gcm:11.0.4'
    compile 'com.android.support:appcompat-v7:26.0.0-beta2'
    compile 'com.android.support:support-v4:26.0.0-beta2'
    compile 'com.android.support:support-v13:26.0.0-beta2'
    compile 'com.googlecode.libphonenumber:libphonenumber:8.7.1'
    compile (name:'liblinphone-android-sdk', ext:'aar')
}
